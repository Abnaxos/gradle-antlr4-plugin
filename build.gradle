apply plugin:'wrapper'
apply plugin:'groovy'
apply plugin:'idea'
apply plugin:'signing'
apply plugin:'maven'

wrapper {
    gradleVersion = '2.4'
}

buildDir = project.file('target')

group = 'ch.raffael.gradlePlugins.antlr4'
version = '1.0-SNAPSHOT'

archivesBaseName = 'gradle-antlr4-plugin'

sourceCompatibility='1.7'

repositories {
    jcenter()
}

configurations {
    antlrTool
}

dependencies {
//    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.10'
    compile gradleApi()

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3', {
        exclude module:'groovy-all'
    }

    antlrTool group: 'org.antlr', name: 'antlr4', version: '4.5'
}

sourceSets {
    main.compileClasspath += configurations.antlrTool
    test.compileClasspath += configurations.antlrTool
}

idea.module {
    scopes.PROVIDED.plus += [ configurations.antlrTool ]
}

test {
    dependsOn jar
    systemProperties['pluginJar'] = jar.archivePath as String
}

task sourcesJar(type:Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}
task javadocJar(type:Jar) {
    classifier 'javadoc'
    from groovydoc
}
artifacts {
    archives sourcesJar, javadocJar
}
signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            def githubName = 'gradle-antlr4-plugin'
            pom.project {
                name 'Gradle ANTLR4 Plugin'
                description 'A Gradle-Plugin for ANTLR4'
                url "https://github.com/Abnaxos/${githubName}"
                scm {
                    connection "scm:git:https://github.com/Abnaxos/${githubName}.git"
                    developerConnection "scm:git:ssh:git@github.com:Abnaxos/${githubName}.git"
                    url "https://github.com/Abnaxos/${githubName}"
                }
                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'Abnaxos'
                        name 'Raffael Herzog'
                        email 'herzog@raffael.ch'
                    }
                }
            }
        }
    }
}
